program romsinst2seq

! --------------------------------------------------
! Program for converting ROMS output NetCDF files
! to met.no's sequential file format
!
! Bjørn Ådlandsvik, <bjorn@imr.no>
! Institute of Marine Research
! 04 March 2005
!
! Modified to also be able to have output in subarea
! by Jon Albretsen, met.no
!
! Also include the most common ice variables (see switch in sup-file)
! by Jon Albretsen, met.no
! -------------------------------------------------

use setup        ! Handle setup file
use ncutil       ! Utilities for NetCDF input 
use time_module  ! Handle time

implicit none

integer, parameter :: sunit = 51  ! Unit for seq. output file

integer :: nc          ! id for the ROMS file

integer :: L, M, N     ! dimensins in ROMS file

real, dimension(:,:),   allocatable :: H, mask_rho  ! Depth, land-mask
real :: hc  
real, dimension(:),     allocatable :: sc_r, cs_r
real, dimension(:,:,:), allocatable :: Z     ! Depth of s-level


real, dimension(:,:),   allocatable :: F2    ! 2D field from ROMS
real, dimension(:,:),   allocatable :: F2U   ! 2D field in U-points
real, dimension(:,:),   allocatable :: F2V   ! 2D field in V-points
real, dimension(:,:,:), allocatable :: F3    ! 3D field from ROMS
real, dimension(:,:,:), allocatable :: F3U   ! 3D field in U-points
real, dimension(:,:,:), allocatable :: F3V   ! 3D field in V-points

integer :: imin, jmin, imax, jmax   ! Dimensions for sequential file

integer(kind=2), dimension(:,:), allocatable :: ifelt
                             ! Scaled data for sequential file

integer :: status     ! Error status from NetCDF library calls

real :: scale         ! Scale factor for sequential storage

integer :: i, j, k    ! Space indices
integer :: ntimes     ! Number of time steps
integer :: tstep      ! Time index
character(len=7) :: roms_time_units
type(time_type)  :: roms_time_origin, roms_time
type(time_type)  :: seq_time_origin
integer, dimension(6) :: tvec

character(len=40) :: tstring

! ---------------------------------------------------------

! ------------------------------
! Read the setup file
! ------------------------------

call readsup('romsinst2seq.sup')

print *, identi

! ------------------------------------
! Open ROMS file and allocate arrays
! ------------------------------------

print *, "romsfile = ", romsfile
status = nf_open(trim(romsfile), NF_NOWRITE, nc)
call check_err(status)

L = dimlen(nc, 'xi_rho')-1
M = dimlen(nc, 'eta_rho')-1
N = dimlen(nc, 'N')

allocate(F2(0:L,0:M), F2U(1:L,0:M), F2V(0:L,1:M), F3(0:L,0:M,N), F3U(1:L,0:M,N), F3V(0:L,1:M,N))

! ------------------------------------
! ROMS s-coordinate 
! ------------------------------------

allocate(H(0:L,0:M), sc_r(N), cs_r(N), Z(0:L,0:M,N))
allocate(mask_rho(0:L,0:M))

call getvar(nc, "h", H)
call getvar(nc, "s_rho", sc_r)
call getvar(nc, "Cs_r", cs_r)
call getvar(nc, "hc",   hc)
print *, "hc = ", hc
! Compute the vertical coordinate structure
do k = 1, N
  Z(:,:,k)   = hc * sc_r(k) + (H-hc) * cs_r(k)
end do
call getvar(nc, "mask_rho", mask_rho)

! -------------------------------------------------------
! Output dimensions, array allocation and file opening
! -------------------------------------------------------
imin = 1
jmin = 1
imax = L-1
jmax = M-1

! Quality control
if ((imax /= identi(10)) .or. (jmax /= identi(11))) then
  write(*,*) "ROMS file size do not match identi from setup file"
  stop
end if

allocate(ifelt(imax,jmax))

open(unit=sunit, file=seqfile, action='write',         & 
     access='sequential', form='unformatted')

! -------------------------------------------
! Time handling
! -------------------------------------------

! Get ntimes, time_units, and time_origin from ROMS file
ntimes = dimlen(nc, 'ocean_time')
call getatt(nc, "ocean_time", "units", tstring)
if (tstring(1:1) == "d") then
  roms_time_units = "days"
  tstring = tstring(12:)
else if (tstring(1:1) == "h") then
  roms_time_units = "hours"
  tstring = tstring(13:)
else if (tstring(1:1) == "s") then
  roms_time_units = "seconds"
  tstring = tstring(15:)
else
  print *, "***Illegal units attribute: ", tstring
  stop
end if
roms_time_origin = tstring(1:19)
print *, tstring(1:19)
call time2str(tstring(1:19), roms_time_origin)
print *, tstring(1:19)
print *, "roms_time_origin = ", roms_time_origin

tvec(1) = identi(12)
tvec(2) = identi(13) / 100
tvec(3) = mod(identi(13), 100_2)
tvec(4) = identi(14) / 100
tvec(5:6) = 0
seq_time_origin = tvec
print *, "seq_time_origin  = ", seq_time_origin

if (identi(9) /= 1) STOP "Error, program not coded for other grids than polar stereographic!"

if ( (i1 == 0) .AND. (i2 == 0) .AND. (j1 == 0) .AND. (j2 == 0) ) then
  i1 = imin
  i2 = imax
  j1 = jmin
  j2 = jmax
endif

!// Change identi(10:11) and identi(15:18) if output on subarea
if ( (i1 > 0) .AND. (i2 > 0) .AND. (j1 > 0) .AND. (j2 > 0) ) then
  if ( (i1 < i2) .AND. (i2 < imax) .AND. (j1 < j2) .AND. (j2 < jmax) ) then
    print *, "Output on sigma-levels on subarea:", i1, i2, j1, j2
    identi(10) = i2 - i1 + 1
    identi(11) = j2 - j1 + 1
    identi(15) = identi(15) - i1 + 1
    identi(16) = identi(16) - j1 + 1
    print *, "New identities on output file:"
    print *, "identi(10:11)=", identi(10:11)
    print *, "identi(15:18)=", identi(15:18)
  endif
endif

! ------------------------------------------
! Time loop
! ------------------------------------------

do tstep = 1, ntimes

  ! -------------------------
  ! Set time 
  ! -------------------------
  call get_time(tstep, roms_time)
  !!!print *, tstep, timestr(roms_time)
  call time2str(tstring, roms_time)
  print *, tstep, tstring

  identi(4) = nint(24*(roms_time - seq_time_origin))
  print *, "identi(4) = ", identi(4)
  if (identi(4) > 0) then
    identi(3) = 2
  end if

  ! -------------------------
  ! Sea surface elevation
  ! -------------------------
  call getvar2dtime(nc, 'zeta', tstep, F2)

  identi(5)  = 8
  identi(6)  = 301
  identi(7)  = 0
  identi(20) = -3  
  scale = 10**(-identi(20))

  ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
  ! Handle land points
  !print *, "mask_rho = ", mask_rho(200,400)
  where(mask_rho(imin:imax,jmin:jmax) < 0.5)
    ifelt = -32767
  end where

  write(sunit) identi
  write(sunit) ifelt(i1:i2,j1:j2)

  ! -------------------------
  ! Sea ice variables
  ! -------------------------

  if (seaice == 1) then

    ! -------------------------
    ! Ice age
    ! -------------------------
    call getvar2dtime(nc, 'ageice', tstep, F2)

    identi(5)  = 8
    identi(6)  = 339
    identi(7)  = 0
    identi(19) = 0
    identi(20) = -1
    scale = 10**(-identi(20))

    ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
    where(mask_rho(imin:imax,jmin:jmax) < 0.5)
      ifelt = -32767
    end where

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)

    ! -------------------------
    ! Sea ice fraction
    ! -------------------------
    call getvar2dtime(nc, 'aice', tstep, F2)
    F2 = F2 * 100. ! convert from fraction to %
    identi(5)  = 8
    identi(6)  = 340
    identi(7)  = 0
    identi(19) = 0
    identi(20) = -2
    scale = 10**(-identi(20))

    ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
    where(mask_rho(imin:imax,jmin:jmax) < 0.5)
      ifelt = -32767
    end where

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)

    ! -------------------------
    ! Sea ice thickness
    ! -------------------------
    call getvar2dtime(nc, 'hice', tstep, F2)

    identi(5)  = 8
    identi(6)  = 341
    identi(7)  = 0
    identi(19) = 0
    identi(20) = -3
    scale = 10**(-identi(20))

    ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
    where(mask_rho(imin:imax,jmin:jmax) < 0.5)
      ifelt = -32767
    end where

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)

    ! -------------------------
    ! Sea ice u-velocity
    ! -------------------------
    call getvar2dtime(nc, 'uice', tstep, F2U)
    F2(imin:imax,:) = 0.5*(F2U(imin:imax,:)+F2U(imin+1:imax+1,:))

    identi(5)  = 8
    identi(6)  = 342
    identi(7)  = 0
    identi(19) = 0
    identi(20) = -4
    scale = 10**(-identi(20))

    ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
    where(mask_rho(imin:imax,jmin:jmax) < 0.5)
      ifelt = -32767
    end where

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)

    ! -------------------------
    ! Sea ice v-velocity
    ! -------------------------
    call getvar2dtime(nc, 'vice', tstep, F2V)
    F2(:,jmin:jmax) = 0.5*(F2V(:,jmin:jmax)+F2V(:,jmin+1:jmax+1))

    identi(5)  = 8
    identi(6)  = 343
    identi(7)  = 0
    identi(19) = 0
    identi(20) = -4
    scale = 10**(-identi(20))

    ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
    where(mask_rho(imin:imax,jmin:jmax) < 0.5)
      ifelt = -32767
    end where

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)

    ! -------------------------
    ! Snow thickness
    ! -------------------------
    call getvar2dtime(nc, 'hsn', tstep, F2)

    identi(5)  = 8
    identi(6)  = 347
    identi(7)  = 0
    identi(19) = 0
    identi(20) = -4
    scale = 10**(-identi(20))

    ifelt = nint(scale*F2(imin:imax,jmin:jmax), kind=2)
    where(mask_rho(imin:imax,jmin:jmax) < 0.5)
      ifelt = -32767
    end where

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)

  endif  ! (seaice == 1)

  ! --------------------------
  ! U-velocity
  ! --------------------------
  call getvar3dtime(nc, 'u', tstep, F3U)
  ! Interpolate to rho-points
  ! ...could improve handling of land ??
  F3(imin:imax,:,:) = 0.5*(F3U(imin:imax,:,:)+F3U(imin+1:imax+1,:,:))

  identi(5)  = 5
  identi(6)  = 302
  identi(20) = -2
  scale = 10**(-identi(20))

  do k = 1, kmax
    identi(7) = nint(zlev(k), kind=2)
    ifelt = nint(scale*s2z(F3(imin:imax,jmin:jmax,:), -zlev(k)), kind=2)

    if (k == 1) then
      ! surface layer, use mask
      where(mask_rho(imin:imax,jmin:jmax) < 0.5)
        ifelt = -32767
      end where
    else
      ! use mask and zlev
      where(mask_rho(imin:imax,jmin:jmax) < 0.5 .OR. H(imin:imax,jmin:jmax) < zlev(k))
        ifelt = -32767
      end where
    end if

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)
  enddo 

  ! --------------------------

  ! V-velocity
  ! --------------------------
  call getvar3dtime(nc, 'v', tstep, F3V)
  ! Interpolate to rho-points
  ! ...could improve handling of land ??
  ! ...could improve handling of land ??
  F3(:,jmin:jmax,:) = 0.5*(F3V(:,jmin:jmax,:)+F3V(:,jmin+1:jmax+1,:))

  identi(6)  = 303
  identi(20) = -2
  scale = 10**(-identi(20))

  do k = 1, kmax
    identi(7) = nint(zlev(k), kind=2)
    ifelt = nint(scale*s2z(F3(imin:imax,jmin:jmax,:), -zlev(k)), kind=2)

    if (k == 1) then
      ! surface layer, use mask
      where(mask_rho(imin:imax,jmin:jmax) < 0.5)
        ifelt = -32767
      end where
    else
      ! use mask and zlev
      where(mask_rho(imin:imax,jmin:jmax) < 0.5 .OR. H(imin:imax,jmin:jmax) < zlev(k))
        ifelt = -32767
      end where
    end if

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)
  enddo 
    
  ! --------------------------
  ! Salinity
  ! --------------------------
  call getvar3dtime(nc, 'salt', tstep, F3)

  identi(6)  = 307
  identi(20) = -2
  scale = 10**(-identi(20))

  do k = 1, kmax
    identi(7) = nint(zlev(k), kind=2)
    ifelt = nint(scale*s2z(F3(imin:imax,jmin:jmax,:), -zlev(k)), kind=2)

    if (k == 1) then
      ! surface layer, use mask
      where(mask_rho(imin:imax,jmin:jmax) < 0.5)
        ifelt = -32767
      end where
    else
      ! use mask and zlev
      where(mask_rho(imin:imax,jmin:jmax) < 0.5 .OR. H(imin:imax,jmin:jmax) < zlev(k))
        ifelt = -32767
      end where
    end if

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)
  enddo 
    
  ! --------------------------
  ! Temperature
  ! --------------------------
  call getvar3dtime(nc, 'temp', tstep, F3)

  identi(6)  = 308
  identi(20) = -2
  scale = 10**(-identi(20))

  do k = 1, kmax
    identi(7) = nint(zlev(k), kind=2)
    ifelt = nint(scale*s2z(F3(imin:imax,jmin:jmax,:), -zlev(k)), kind=2)

    if (k == 1) then
      ! surface layer, use mask
      where(mask_rho(imin:imax,jmin:jmax) < 0.5)
        ifelt = -32767
      end where
    else
      ! use mask and zlev
      where(mask_rho(imin:imax,jmin:jmax) < 0.5 .OR. H(imin:imax,jmin:jmax) < zlev(k))
        ifelt = -32767
      end where
    end if

    write(sunit) identi
    write(sunit) ifelt(i1:i2,j1:j2)
  enddo 
    
end do ! tstep

! ----------------------
! Final clean-up
! ----------------------
close(sunit)
status = nf_close(nc)
call check_err(status)

contains

  function s2z(F, z0) result(G)
  ! Estimates 3D field at level G by linear interpolation
    real, dimension(imax,jmax,N), intent(in) :: F
    real, intent(in) :: z0
    real, dimension(imax, jmax) :: G
    integer :: ks
    real :: a

    do j = 1, jmax
      do i = 1, imax
        if (z0 < Z(i,j,1)) then
          G(i,j) = F(i,j,1)
        else if (z0 >= Z(i,j,N)) then
          G(i,j) = F(i,j,N)
        else ! Z(1) <= z0 < Z(N)
          do ks = 2, N
            if (z0 < Z(i,j,ks)) then ! Z(ks-1) <= z0 < Z(ks)
              a = (Z(i,j,ks)-z0) / (Z(i,j,ks) - Z(i,j,ks-1))
              G(i,j) = a*F(i,j,ks-1) + (1-a)*F(i,j,ks)
              exit
            endif ! (z0 < Z(i,j,ks)
          enddo ! ks
        endif ! (z0 < Z(i,j,1)
      enddo ! i
    enddo ! j
  
  end function s2z
    
  subroutine get_time(tstep, ocean_time)
    ! Return the time stamp of timestep=tstep in 
    ! a open ROMS file

    integer, intent(in) :: tstep
    type(time_type), intent(out) :: ocean_time
     
    real(kind=8) :: otime

    call getvartime(nc, "ocean_time", tstep, otime)
    print *, "otime = ", otime

    if (roms_time_units == "seconds") then
      otime = otime / (24*3600)
    else if (roms_time_units == "hours") then
      otime = otime / 24
    end if
    print *, "otime = ", otime
    ocean_time = roms_time_origin + otime

  end subroutine get_time

end program romsinst2seq
