#! /bin/csh -f

### Change these to your own site and user directory! 
### You will need to create a Makefile Macro in bld/ and a run_ice script 
### in input_templates/.

if ( $METROMS_MYHOST == "metlocal" ) then
    setenv SITE Ubuntu 
else if ( $METROMS_MYHOST == "vilje" ) then
    setenv SITE NTNU.vilje
else
    echo $METROMS_MYHOST not defined
    exit
endif

### SYSTEM_USERDIR is predefined on ORNL machines
setenv SYSTEM_USERDIR $CICE_DIR

### Grid resolution
setenv RES A20 ; setenv GRID 322x242
#setenv RES col ; setenv GRID 5x5
#setenv RES gx3 ; setenv GRID 100x116
#setenv RES gx1 ; setenv GRID 320x384
#setenv RES tx1 ; setenv GRID 360x240

@ NPX =  $1   # number of processors in x direction
@ NPY =  $2   # number of processors in y direction
@ BPX =  1   
@ BPY =  1   

@ NTASK = ($NPX * $NPY)   # total number of processors

@ NXGLOB = `echo $GRID | sed s/x.\*//`
@ NYGLOB = `echo $GRID | sed s/.\*x//`

# Recommendations:
#   NTASK equals nprocs in ice_in 
#   use processor_shape = slenderX1 or slenderX2 in ice_in
#   one per processor with distribution_type='cartesian' or
#   squarish blocks with distribution_type='rake'
# If BLCKX (BLCKY) does not divide NXGLOB (NYGLOB) evenly, padding 
# will be used on the right (top) of the grid.

### x grid decomposition
@ a = ($NXGLOB / $NPX) 
@ rem1 = ($NXGLOB % $NPX) 
@ b = ($a + 1)

if ($rem1 == 0) then 
  setenv BLCKX $a 
else 
  setenv BLCKX $b 
endif

### y grid decomposition
@ a = ($NYGLOB / $NPY) 
@ rem1 = ($NYGLOB % $NPY) 
@ b = ($a + 1)

if ($rem1 == 0) then 
  setenv BLCKY $a 
else 
  setenv BLCKY $b 
endif

@ MXBLCKS = ($BPX * $BPY)

setenv NICELYR    7       # number of vertical layers in the ice
setenv NSNWLYR    1       # number of vertical layers in the snow
setenv NICECAT    5       # number of ice thickness categories

### Tracers               # match ice_in tracer_nml to conserve memory
setenv TRAGE   1          # set to 1 for ice age tracer
setenv TRFY    1          # set to 1 for first-year ice area tracer
setenv TRLVL   1          # set to 1 for level and deformed ice tracers
setenv TRPND   1          # set to 1 for melt pond tracers
setenv NTRAERO 0          # number of aerosol tracers 
                          # (up to max_aero in ice_domain_size.F90) 
                          # CESM uses 3 aerosol tracers
setenv TRBRI   0          # set to 1 for brine height tracer
setenv NBGCLYR 7          # number of zbgc layers
setenv TRBGCS  0          # number of skeletal layer bgc tracers 
                          # TRBGCS=0 or 2<=TRBGCS<=9)

### Specialty code
setenv CAM_ICE  no        # set to yes for CAM runs (single column) 
setenv SHRDIR   csm_share # location of CCSM shared code
setenv IO_TYPE  netcdf    # set to none if netcdf library is unavailable
                          # set to pio for parallel netcdf
setenv DITTO    no        # reproducible diagnostics
setenv BARRIERS no        # prevent MPI buffer overflow during gather/scatter 
setenv THRD     no        # set to yes for OpenMP threading

if ( $THRD == 'yes') setenv OMP_NUM_THREADS 2 # positive integer 

### File unit numbers
#setenv NUMIN 11           # minimum file unit number
#setenv NUMAX 99           # maximum file unit number
# Redefiend when coupled with ROMS (JD: 20150116), std_in,std_out goes to 505,506, respectively.
setenv NUMIN 511           # minimum file unit number
setenv NUMAX 599           # maximum file unit number

### Set SRCDIR and EXEDIR to your own paths!
setenv SRCDIR $SYSTEM_USERDIR

setenv EXEDIR $SYSTEM_USERDIR/rundir
                                          if !(-d $EXEDIR) mkdir -p $EXEDIR
setenv CBLD   $SRCDIR/bld
setenv OBJDIR $EXEDIR/compile           ; if !(-d $OBJDIR) mkdir -p $OBJDIR
setenv RSTDIR $EXEDIR/restart           ; if !(-d $RSTDIR) mkdir -p $RSTDIR
setenv HSTDIR $EXEDIR/history           ; if !(-d $HSTDIR) mkdir -p $HSTDIR

setenv ARCH `uname -s`
if ( $ARCH == 'UNICOS/mp') setenv ARCH UNICOS
if ( $ARCH == 'UNICOS') then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else if ( $ARCH == 'Darwin' ) then
   cp -f $CBLD/Makefile.$ARCH $CBLD/Makefile
else
   cp -f $CBLD/Makefile.std $CBLD/Makefile
endif
setenv ARCH $ARCH.$SITE

cd $EXEDIR
cp $SRCDIR/input_templates/$RES/cice.grid.nc .
cp $SRCDIR/input_templates/$RES/cice.kmt.nc .

sed "s#<cicedir>#$SYSTEM_USERDIR#;s#<ntask>#$NTASK#" $SRCDIR/input_templates/$RES/ice_in > $EXEDIR/ice_in

cd $RSTDIR
cp $SRCDIR/input_templates/$RES/iced_$RES* .
if !(-e ice.restart_file) cp $SRCDIR/input_templates/$RES/ice.restart_file .

cd $OBJDIR

# Always needs mpi for coupled runs
#if ($NTASK == 1) then
#   setenv COMMDIR serial
#else
   setenv COMMDIR mpi 
#endif

setenv DRVDIR cice
if ($IO_TYPE == 'netcdf') then
  setenv IODIR io_netcdf
else if ($IO_TYPE == 'pio') then
  setenv IODIR io_pio
else
  setenv IODIR io_binary
endif

### List of source code directories (in order of importance).
cat >! Filepath << EOF
$SRCDIR/drivers/$DRVDIR
$SRCDIR/source
$SRCDIR/$COMMDIR
$SRCDIR/$IODIR
$SRCDIR/$SHRDIR
EOF

cc -o makdep $CBLD/makdep.c                         || exit 2

make  VPFILE=Filepath EXEC=$EXEDIR/cice \
           NXGLOB=$NXGLOB NYGLOB=$NYGLOB \
           BLCKX=$BLCKX BLCKY=$BLCKY MXBLCKS=$MXBLCKS \
      -f  $CBLD/Makefile MACFILE=$CBLD/Macros.$ARCH  || exit 2

cd ..
pwd                                         
echo NTASK = $NTASK
echo "global N, block_size" 
echo "x    $NXGLOB,    $BLCKX"
echo "y    $NYGLOB,    $BLCKY"
echo max_blocks = $MXBLCKS
echo $TRAGE   = TRAGE,   iage tracer
echo $TRFY    = TRFY,    first-year ice tracer
echo $TRLVL   = TRLVL,   level-ice tracers
echo $TRPND   = TRPND,   melt pond tracers
echo $NTRAERO = NTRAERO, number of aerosol tracers
echo $TRBRI   = TRBRI,   brine height tracer
echo $NBGCLYR = NBGCLYR, number of bio grid layers
echo $TRBGCS  = TRBGCS,  number of BGC tracers

